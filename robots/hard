birth
	PrintMarketInfo()
	PrintMyInfo()
\Produce products
	int pp(0)
	if (MyRaw < MyFact + MyAfact*2) {
		pp = MyRaw;
	} else {
		pp = MyFact + MyAfact*2;
	}
	if (pp*2000 >= MyMoney) {
		pp = MyMoney / 2000;
	}
	prod(pp)
\Sell products
	int i(0), SumProd(0), sp(0)
	while((i = i + 1) <= MaxNum) {				\Get product another players
		if (PlayerActive(i)) {
			SumProd = SumProd + PlayerProd(i);
		}
	}
	if (SumProd <= MaxProd) {
		sell(MyProd, MaxProdPrice)
	} else {
		if (MaxProd < MyProd) {
			sp = MaxProd;
		} else {
			sp = MyProd;
		}
		i = 0;
		int SoldDif(0), num(0)
		while((i = i + 1) <= MaxNum) {			\Get difference in sold price 
			if (PlayerActive(i) and i =! MyNum and PlayerDifSold(i) > 0) {
				SoldDif = SoldDif + PlayerDifSold(i);
				num = num + 1;
			}
		}
		put(SoldDif)
		if (num) {
			sell(sp, MaxProdPrice - (SoldDif / num + 500))
		} else {
			sell(sp, MaxProdPrice - 108)
		}
	}
\Buy raws
	int remain(MyMoney - pp*2000 - (MyRaw - pp)*300 - MyProd*500)
	remain = remain - MyFact*1000 - MyAfact*1500;
	if (remain > 0) {
		i = 0;
		int BoughtDif(0), num(0), BoughtPrice(0)
		while((i = i + 1) <= MaxNum) {			\Get difference in bought price 
			if (PlayerActive(i) and i =! MyNum and PlayerDifBought(i) > 0) {
				BoughtDif = BoughtDif + PlayerDifBought(i);
				num = num + 1;
			}
		}
		put(BoughtDif)
		if (num) {
			BoughtPrice = MinRawPrice + BoughtDif / num + 108;
		} else {
			BoughtPrice = MinRawPrice + 108;
		}
		int BoughtRaw(MyFact + MyAfact*2 - (MyRaw - pp))
		if (BoughtRaw > MaxRaw) {
			BoughtRaw = MaxRaw;
		}
		if (BoughtPrice * BoughtRaw > remain) {
			buy(remain / BoughtPrice, BoughtPrice)
		} else {
			buy(BoughtRaw, BoughtPrice)
		}
		remain = remain - BoughtRaw * BoughtPrice;
	}
\Build factories
	if (remain > 30000 and MyFact + MyAfact*2 < 6) {
		build
	}
death
